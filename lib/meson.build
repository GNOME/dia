stdprop_sources = [
    'properties.c',
    'propdesc.c',
    'proplist.c',
    'propoffsets.c',
    'propobject.c',
    'prop_basic.c',
    'prop_dict.c',
    'prop_matrix.c',
    'prop_pattern.c',
    'prop_pixbuf.c',
    'prop_inttypes.c',
    'prop_geomtypes.c',
    'prop_attr.c',
    'prop_text.c',
    'prop_widgets.c',
    'prop_sdarray.c',
    'prop_sdarray_widget.c',
    'propdialogs.c',
    'propregistry.c'
]

libdia_sources = stdprop_sources + [
    'diagramdata.c',
    'layer.c',
    'geometry.c',
    'color.c',
    'dia_xml.c',
    'attributes.c',
    'text.c',
    'textline.c',
    'font.c',
    'group.c',
    'utils.c',
    'arrows.c',
    'message.c',
    'focus.c',
    'sheet.c',
    'connectionpoint.c',
    'object.c',
    'object-alias.c',
    'object_defaults.c',
    'connection.c',
    'orth_conn.c',
    'poly_conn.c',
    'bezier_conn.c',
    'bezier-common.c',
    'element.c',
    'polyshape.c',
    'beziershape.c',
    'objchange.c',
    'dialogs.c',
    'widgets.c',
    'diaoptionmenu.c',
    'units.c',
    'dia_image.c',
    'pattern.c',
    'standard-path.c',
    'intl.c',
    'filter.c',
    'connpoint_line.c',
    'dia_dirs.c',
    'plug-ins.c',
    'paper.c',
    'boundingbox.c',
    'dynamic_obj.c',
    'create.c',
    'diatransform.c',
    'diarenderer.c',
    'diaimportrenderer.c',
    'diainteractiverenderer.c',
    'diagdkrenderer.c',
    'diapathrenderer.c',
    'path-math.c',
    'diapatternselector.c',
    'diatransformrenderer.c',
    'diasvgrenderer.c',
    'dia_svg.c',
    'autoroute.c',
    'parent.c',
    'render_pixmap.c',
    'diaarrowchooser.c',
    'diaarrowselector.c',
    'diacolorselector.c',
    'dialinestyleselector.c',
    'dialinechooser.c',
    'diadynamicmenu.c',
    'diafontselector.c',
    'persistence.c',
    'diaerror.c',
    'debug.c',
    'prefs.c',
    'dialib.c',
    'diacontext.c',
    'diacellrendererenum.c',
    'diacellrendererproperty.c',
]

# Note glib-genmarshal supports --output argument, but only after version 2.53.2
glib_genmarshal = find_program('glib-genmarshal')
diamarshal_c = custom_target(
    'diamarshal.c',
    input : 'diamarshal.list',
    output : 'diamarshal.c',
    capture : true,
    command : [ glib_genmarshal,
                '--body',
                '--prefix=dia_marshal',
                '@INPUT@' ]
)

diamarshal_h = custom_target(
    'diamarshal.h',
    input : 'diamarshal.list',
    output : 'diamarshal.h',
    capture : true,
    command : [ glib_genmarshal,
                '--header',
                '--prefix=dia_marshal',
                '@INPUT@' ]
)

subdir('pixmaps')

libdia_deps = [
    libc_dep,
    libgtk_dep,
    libglib_dep,
    libxml_dep,
    libm_dep,
    libzlib_dep,
    gmodule_dep,
]

libdia_inc = include_directories('.')

libdia = library('dia',
    libdia_sources + [diamarshal_c, diamarshal_h, dia_lib_icons_h],
    dependencies : libdia_deps,
    include_directories : configuration_inc,
    c_args : ['-Wall'],
    vs_module_defs: files('libdia.def')[0],
    install : true,
)
